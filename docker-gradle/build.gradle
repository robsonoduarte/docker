apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'docker'


// see -> http://stackoverflow.com/questions/26916081/how-to-build-a-mixed-java-scala-project
sourceSets.main.scala.srcDir "src/main/java"
sourceSets.test.scala.srcDir "src/test/java"
sourceSets.main.java.srcDirs = []
sourceSets.test.java.srcDirs = []


buildscript {
    repositories { mavenLocal(); jcenter() }
    dependencies {
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}


repositories {
    jcenter()
}

dependencies {
	compile 'org.scala-lang:scala-library:2.11.8'
	testCompile 'junit:junit:4.12'
}



// see -> http://www.mkyong.com/gradle/gradle-create-a-jar-file-with-dependencies/
jar {
    manifest {
        attributes 'Main-Class': 'br.com.mystudies.docker.gradle.Echo'
    }
     from {
    	configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  	}
}



/*
   DOCKER

   For more information about Gradle Docker plguin see:

   https://github.com/Transmode/gradle-docker
*/

docker {
   // the official image to java -> https://hub.docker.com/_/java/
   baseImage 'java:8-jre-alpine'
}


task dockerImage(type: Docker, dependsOn: ['jar', 'startFile']) {
	runCommand 'apk add --update bash && rm -rf /var/cache/apk/*'
	addFile jar.archivePath, '/' + jar.archiveName
	addFile 'build/start.sh', '/start.sh'
	runCommand 'chmod +x /start.sh'
	defaultCommand = ['/start.sh' ]
}



Map<String, String> hostnames = [lo: 'localhost', de: '192.168.99.100']


// see -> http://mrhaki.blogspot.com.br/2010/10/gradle-goodness-copy-files-with.html
import org.apache.tools.ant.filters.*
task startFile(type: Copy){
   from 'src/main/resources'
   into buildDir
   include '**/*.sh'
   filter(ReplaceTokens, tokens:[
	   		archiveName: jar.archiveName,
	   		environment: System.properties['environment'],
	   		hostname: hostnames[System.properties['environment']]
   		]
   	)
}

